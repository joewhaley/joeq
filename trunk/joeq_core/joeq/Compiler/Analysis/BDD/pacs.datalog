
### Domains

.include "fielddomains.pa"

.bddvarorder N0_F0_I0_M1_M0_V1xV0_VC1xVC0_T0_Z0_T1_H0_H1

### Relations

vP0 (v:V0, h:H0) input
vP (vc:VC0, v:V0, h:H0) output
cA (vcdest:VC0, dest:V0, vcsrc:VC1, source:V1) input
hP (ha:H0, field:F0, hb:H1) output
S (base:V0, field:F0, src:V1) input
L (base:V0, field:F0, dest:V1) input
vT (var:V0, type:T0) input
hT (heap:H0, type:T1) input
aT (type:T0, type:T1) input
vPfilter (v:V0, h:H0)
actual (invoke:I0, num:Z0, actualparam:V1) input
formal (method:M0, num:Z0, formalparam:V0) input
Mret (method:M0, v:V1) input
Mthr (method:M0, v:V1) input
Iret (invoke:I0, v:V0) input
Ithr (invoke:I0, v:V0) input
IE0 (invoke:I0, target:M0) input
IEfilter (ccaller:VC1, invoke:I0, ccallee:VC0, target:M0) input
IE (ccaller:VC1, invoke:I0, ccallee:VC0, target:M0) output

### Rules

vP(_,v,h) :- vP0(v,h).
IE(vc2,i,vc1,m) :- IE0(i,m), IEfilter(vc2,i,vc1,m).
vPfilter(v,h) :- vT(v,tv), aT(tv,th), hT(h,th).
vP(vc1,v1,h) :- cA(vc1,v1,vc2,v2), vP(vc2,v2,h), vPfilter(v1,h).
hP(h1,f,h2) :- S(v1,f,v2), vP(vc1,v1,h1), vP(vc1,v2,h2).
vP(vc1,v2,h2) :- L(v1,f,v2), vP(vc1,v1,h1), hP(h1,f,h2), vPfilter(v2,h2). split
cA(vc1,v1,vc2,v2) :- formal(m,z,v1), IE(vc2,i,vc1,m), actual(i,z,v2).
cA(vc2,v2,vc1,v1) :- Mret(m,v1), IE(vc2,i,vc1,m), Iret(i,v2).
cA(vc2,v2,vc1,v1) :- Mthr(m,v1), IE(vc2,i,vc1,m), Ithr(i,v2).

#roots (method:M0) input
#mI (method:M0, invoke:I0, name:N0) input
#mI0 (method:M0, invoke:I0)
#IEnum (invoke:I0, target:M0, ccaller:VC1, ccallee:VC0) output
#mI0(m,i) :- mI(m,i,_).
#IEnum(i,m,vc2,vc1) :- roots(m), mI0(m,i), IE0(i,m). number
